plugins {
	id 'java'
	id 'maven-publish'
	id 'java-gradle-plugin'
	id 'idea'
	id 'eclipse'
	id 'groovy'
	id 'checkstyle'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'io.github.fukkitmc'
archivesBaseName = project.name
version = 'g0.2.6-SNAPSHOT'

def build = "5"

repositories {
	mavenCentral()
	maven {
		name = "Fabric"
		url = 'https://maven.fabricmc.net/'
	}
	maven {
		name = "FukkitMC"
		url = uri("https://raw.githubusercontent.com/FukkitMC/fukkit-repo/master")
	}
}

dependencies {
	implementation gradleApi()

	// libraries
	implementation ('commons-io:commons-io:2.6')
	implementation ('org.zeroturnaround:zt-zip:1.13')
	implementation ('com.google.code.gson:gson:2.8.5')
	implementation ('com.google.guava:guava:28.0-jre')

	// game handling utils
	implementation ('net.fabricmc:stitch:0.4.1.68') {
		exclude module: 'enigma'
	}

	// tinyfile management
	implementation ('io.github.fukkitmc:tiny-remapper:0.2.1.0') {
		transitive = false
	}

	// Gloom
	implementation ('io.github.fukkitmc:gloom:2.0.5')

	// decompilers
	implementation ('net.fabricmc:procyon-fabric-compilertools:0.5.35.+')
	implementation ('org.jetbrains:intellij-fernflower:1.1.0.11')

	// source code remapping
	implementation ('org.cadixdev:mercury:0.1.0.fabric-SNAPSHOT')

	// Testing
	testImplementation(gradleTestKit())
	testImplementation("org.spockframework:spock-core:1.3-groovy-2.4")
}

jar {
	manifest {
		attributes 'Implementation-Version': version + " Build(" + build + ")"
	}
}

apply from: 'https://github.com/FabricMC/fabric-docs/raw/master/gradle/license.gradle'

license {
	exclude '**/loom/util/DownloadUtil.java'
}

checkstyle {
	configFile = file("checkstyle.xml")
	toolVersion = '8.25'
}

checkstyleMain {
	logging.setLevel(LogLevel.LIFECYCLE)
}

gradlePlugin {
	plugins {
		fabricLoom {
			id = "fabric-loom"
			implementationClass = "net.fabricmc.loom.LoomGradlePlugin"
		}
	}
}

publishing {
	repositories {
		maven {
			name = "Fukkit"
			url = uri("../fukkit-repo")
		}
	}
}
